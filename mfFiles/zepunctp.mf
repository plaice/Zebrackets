def lp(expr mark_count, total_count) =
    beginchar(mark_count,7u# if monospace: -u# fi,body_height#,paren_depth#);

	if (foreground = 0):
	    "zebracket- left parentheses, background " & decimal mark_count & "/" & decimal total_count;
	elseif (foreground=1):
	    "zebracket- left parentheses, foreground " & decimal mark_count & "/" & decimal total_count;
	elseif (foreground=2):
	    "zebracket- left parentheses, hybrid " & decimal mark_count & "/" & decimal total_count;
	else:
	    "trouble city: invalid foreground type";
	fi;

	italcorr body_height#*slant-.5u#;
	adjust_fit(0,0); pickup fine.nib;
	pos1(vair,0); pos2(.75[hair,stem],0); pos3(vair,0);
	rt x1r=rt x3r=hround(w-u); lft x2l=hround(x1-4u if monospace: +4/3u fi);
	top y1=h; y2=.5[y1,y3]=math_axis;
	filldraw stroke z1e{3(x2e-x1e),y2-y1}...z2e
	    ...{3(x3e-x2e),y3-y2}z3e;  % arc
	
	if  (foreground=0) or (foreground=2):
	    pickup pensquare scaled .6pt;
	    lft x5 = lft x2 - .1w;
	    rt x6 = rt x1 + .1w;
	fi;

	if (foreground=2):
	    for bitpos := 0 upto (total_count - 1):
		y5 := y6 := ((bitpos+1)/(total_count+1))[y1,y3];
		erase draw z5 -- z6;
	    endfor;
	fi;

	if (foreground=1) or (foreground=2):
	    pickup pencircle scaled .25pt;
	    x5 := lft x2 - .1w;
	    x6 := x1 - .1w;
	fi;

	for bitpos := 0 upto (total_count - 1):
	    if ((mark_count mod (2**(bitpos+1))) >= (2**bitpos)): 
		y5 := y6 := ((bitpos+1)/(total_count+1))[y1,y3];
		if (foreground = 0):
		    erase draw z5 -- z6;
		else:
		    draw z5 -- z6;
		fi;
	    fi;
	endfor;
	
	penlabels(1,2,3,5,6); 
    endchar;
enddef;

def rp(expr mark_count, total_count) =
    beginchar(mark_count + (2**total_count),7u# if monospace: -u# fi,body_height#,paren_depth#);

	if (foreground = 0):
	    "zebracket- right parentheses, background " & decimal mark_count & "/" & decimal total_count;
	elseif (foreground=1):
	    "zebracket- right parentheses, foreground " & decimal mark_count & "/" & decimal total_count;
	elseif (foreground=2):
	    "zebracket- right parentheses, hybrid " & decimal mark_count & "/" & decimal total_count;
	else:
	    "trouble city: invalid foreground type";
	fi;

	italcorr math_axis#*slant-.5u#;
	adjust_fit(0,0); pickup fine.nib;
	pos1(vair,0); pos2(.75[hair,stem],0); pos3(vair,0);
	lft x1l=lft x3l=hround u; rt x2r=hround(x1+4u if monospace: -4/3u fi);
	top y1=h; y2=.5[y1,y3]=math_axis;
	filldraw stroke z1e{3(x2e-x1e),y2-y1}...z2e
	    ...{3(x3e-x2e),y3-y2}z3e;  % arc
	
	if (foreground = 0) or (foreground=2):
	    pickup pensquare scaled .6pt;
	    lft x5 = lft x1 - .1w;
	    rt x6 = rt x2 + .1w;
	fi;

	if (foreground=2):
	    for bitpos := 0 upto (total_count - 1):
		y5 := y6 := ((bitpos+1)/(total_count+1))[y1,y3];
		erase draw z5 -- z6;
	    endfor;
	fi;

	if (foreground=1) or (foreground=2):
	    pickup pencircle scaled .25pt;
	    x5 := x1 + .1w;
	    x6 := rt x2 + .1w;
	fi;

	for bitpos := 0 upto (total_count - 1):
	    if ((mark_count mod (2**(bitpos+1))) >= (2**bitpos)): 
		y5 := y6 := ((bitpos+1)/(total_count+1))[y1,y3];
		if (foreground = 0):
		    erase draw z5 -- z6;
		else:
		    draw z5 -- z6;
		fi;
	    fi;
	endfor;
	
	penlabels(1,2,3,5,6); 
    endchar;
enddef;

for s := 0 upto ((2**stripes)-1):
    lp(s,stripes);
    rp(s,stripes);
endfor;

%tracingequations:=tracingonline:=1;
%lp(1,4);
%rp(1,4);
